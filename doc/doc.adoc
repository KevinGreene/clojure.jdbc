clojure.jdbc documentation
==========================
Andrey Antukh, <niwi@niwi.be>
0.3.1
:toc: left
:numbered:
:source-highlighter: pygments
:pygments-style: friendly


Introduction
------------

_clojure.jdbc_ is a library for low level, jdbc-based database access.


link:api/index.html[Api reference documentation.]

Philosophy
~~~~~~~~~~

Five most important rules:

- Beautiful is better than ugly.
- Explicit is better than implicit.
- Simple is better than complex.
- Complex is better than complicated.
- Readability counts.

All contributions to _clojure.jdbc_ should keep these important rules in mind.


Project Maturity
----------------

Since _clojure.jdbc_ is a young project there may be some API breakage.

Install
-------

This section covers a installing _clojure.jdbc_ and its requirements.

Requirements
~~~~~~~~~~~~

_clojure.jdbc_ is tested with these platforms:

- JDK7
- JDK8

Coming from clj.jdbc
~~~~~~~~~~~~~~~~~~~~

Only a package name is changed, all API is maintained as is with expected
small changes as described in changelog.

You should change your dependency entry on project.clj to new package name:

[source, clojure]
----
[clojure.jdbc "x.y.z"]
----


Leiningen
~~~~~~~~~

The simplest way to use _clojure.jdbc_ in a clojure project, is by including it in the dependency
vector on your *_project.clj_* file:

._on project.clj_
[source,clojure]
----
[clojure.jdbc "0.3.1"]
----

Gradle
~~~~~~

If you are using gradle, this is a dependency line for gradle dsl:

[source,groovy]
----
compile "clojure.jdbc:clojure.jdbc:0.3.1"
----

Maven
~~~~~

And for old school people, who are using a ugly XML files to configure everything,
this is a XML block that you should put in a dependency section of a maven config file:

.Repository entry.
[source,xml]
----
<repository>
    <id>clojars.org</id>
    <url>http://clojars.org/repo</url>
</repository>
----

.Dependency entry.
[source,xml]
----
<dependency>
  <groupId>clojure.jdbc</groupId>
  <artifactId>clojure.jdbc</artifactId>
  <version>0.3.1</version>
</dependency>
----


Get the Code
~~~~~~~~~~~~

_clojure.jdbc_ is open source and is entirely developed on link:https://github.com/niwibe/clojure.jdbc[github].

You can clone the public repository with this command:

[source,text]
----
git clone https://github.com/niwibe/clojure.jdbc
----


User Guide
----------

Connecting to database
~~~~~~~~~~~~~~~~~~~~~~

Connection parameters
^^^^^^^^^^^^^^^^^^^^^

JDBC is the default Java abstraction/interface for SQL databases.  It's like
the Python DB-API and similar abstractions in other languages.  Clojure, as a
guest language on the JVM, benefits from having a good, well-tested abstraction
like that.

Connection parameters are exposed in a simple hash-map and called *dbspec*. This is the simplest
and most idiomatic way in Clojure to define configuration parameters.

.This is a default aspect of one dbspec.
[source,clojure]
----
(def dbspec {:subprotocol "postgresql"
             :subname "//localhost:5432/dbname"
             :user "username"         ;; Optional
             :password "password"}    ;; Optional
----

Also, _clojure.jdbc_ comes with alternative, more human comprehensible format, like this:

.Pretty dbspec format
[source, clojure]
----
(def dbspec {:vendor "postgresql"
             :name "dbname"
             :host "localhost"      ;; Optional
             :port 5432             ;; Optional
             :user "username"       ;; Optional
             :password "password"}) ;; Optional
----

[NOTE]
====
The `:classname` parameter represents a class location/route of the JDBC driver. Each driver has one;
in this example it is a path to a Postgresql JDBC driver.  This parameter can be omited and in that
case it is automatically resolved from a predefined list using the `:subprotocol` key.
====

Also, *dbspec* can be represented as a URI.

.Same as the previous example but using URI format.
[source,clojure]
----
(def dbspec "postgresql://user:password@localhost:5432/dbname")
----


Creating a connection
^^^^^^^^^^^^^^^^^^^^^

With clojure.jdbc every function that interacts with a database explicitly requires
one connection instance as parameter (no dynamic vars are used for it).


NOTE: clojure.jdbc does not use any global/thread-local state, and always tries to ensure immutability.

NOTE: unlike java.jdbc, with _clojure.jdbc_ you can't use a plain dbspec hash-map as a
connection parameter and you should explicitly open a new connection before doing any operation
with a database.

.Example using `make-connection` function
[source,clojure]
----
(require '[jdbc.core :refer :all])

(let [conn (make-connection dbspec)]
  (do-something-with conn)
  (.close conn))
----

The `make-connection` function exposes a low-level interface for creating a connection,
and delegates connection resource management to the user. A connection is not automatically
closed and so use of the `with-open` macro is strongly recommended for clear resource management.

.Better way.
[source,clojure]
----
(with-open [conn (make-connection dbspec)]
  (do-something-with conn))
----

However, the `with-connection` macro intends to be a high-level abstraction and
works like the `with-open` clojure macro. This is an equivalent piece of code using
`with-connection` macro:

[source,clojure]
----
(with-connection [conn dbspec]
  (do-something-with conn))
----


Execute Database Commands
~~~~~~~~~~~~~~~~~~~~~~~~~

Execute Raw SQL Statements
^^^^^^^^^^^^^^^^^^^^^^^^^^

The simplest way to execute raw SQL is using the `execute!` function. It requires
an active connection as the first parameter followed by SQL sentences:

[source,clojure]
----
(with-connection [conn dbspec]
  (execute! conn "CREATE TABLE foo (id serial, name text);"))
----


Execute Parametrized SQL Statements
^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

Raw SQL statements work well for creating tables and similar operations, but
when you need to insert some data, especially if the data comes from untrusted
sources, the `execute!` function is not a good option.

For this problem, clojure.jdbc exposes the `execute-prepared!` function. It
accepts parametrized SQL and a list of groups of parameters that allow
the repeated execution of the same operations with distinct parameters.

.Execute a simple insert SQL statement.
[source,clojure]
----
(let [sql "INSERT INTO foo (name) VALUES (?)"]
  (execute-prepared! conn [sql "Foo"]))
----

.Bulk insert example.
[source,clojure]
----
(let [sql "INSERT INTO foo (name) VALUES (?)"]
  (execute-prepared! conn sql ["Foo"] ["Bar"]))

;; This should emit this sql:
;;   INSERT INTO foo (name) VALUES ('Foo');
;;   INSERT INTO foo (name) VALUES ('Bar');
----


Returning Inserted Keys
^^^^^^^^^^^^^^^^^^^^^^^

In some circumstances, you want use "RETURNING id" or similar functionality on
your queries for return the primary keys of newly inserted records.

This is how you can do it using clojure.jdbc:

[source, clojure]
----
(let [sql "INSERT INTO foo (name) VALUES (?);"
      res (execute-prepared! conn sql ["Foo"] ["Bar"] {:returning [:id]})]
  (println res))

;; This should print something like this to standard output:
[{:id 3} {:id 4}]
----

Also, you can use `:all` keyword in case of you need statement with "RETURNING *".

[source, clojure]
----
(execute-prepared! conn sql ["foo"] {:returning :all})
----

Make Queries
~~~~~~~~~~~~

The basic way to query a database is using the `query` function:

[source,clojure]
----
(let [sql    ["SELECT id, name FROM people WHERE age > ?", 2]
      result (query conn sql)]
  (doseq [row results]
  (println row))))

;; It should print this:
;; => {:id 1 :name "Foo"}
;; => {:id 2 :name "Bar"}
----

Parametrized sql can be:

- A vector with first element a sql string following with parameters
- A native string (SQL query without parameters)
- An instance of `PreparedStatement`
- An instance of any type that implements the `ISQLStatement` protocol.

[NOTE]
====
This method seems useful in most cases but may not work well with
queries that returns a lot of results. For this purpose, cursor type queries exist
that are explained in the xref:cursor-queries[Advanced usage] section.
====


Transactions
~~~~~~~~~~~~

Getting Started with Transactions
^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

All transaction related functions in _clojure.jdbc_ are exposed under the `jdbc.transaction`
namespace and if you need transactions in your code, you should import it:

[source,clojure]
----
(require '[jdbc.transaction :as tx])
----

The most idiomatic way to wrap some code in a transaction, is by using the `with-transaction`
macro:

[source,clojure]
----
(tx/with-transaction conn
  (do-thing-first conn)
  (do-thing-second conn))
----

[NOTE]
====
_clojure.jdbc_ does not uses any dynamic thread-local vars to store the transaction state
of a connection. Instead of that, it overwrites the lexical scope value of `conn` with a new
connection that has transactional state.
====


Low-level Transaction Primitives
^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

Behind the scenes of the `with-transaction` macro, _clojure.jdbc_ uses the `call-in-transaction`
function.

Given an active connection as the first parameter and function that you want execute in a
transaction as the second parameter, it executes the function inside a database transaction.
The function should accept a connection as its first parameter.

[source,clojure]
----
(tx/call-in-transaction conn (fn [conn] (do-something-with conn)))
----


[NOTE]
====
clojure.jdbc, in contrast to java.jdbc, handles nested transactions well. Thus making all
code wrapped in transaction blocks truly atomic independently of transaction nesting.

If you want extend or change a default transaction strategy, see
xref:transaction-strategy[Transaction Strategy section].
====


Isolation Level
^^^^^^^^^^^^^^^

clojure.jdbc by default does nothing with the isolation level and keeps it to default values.

.You can set the isolation level when creating a connection by specifying it in your dbspec.
[source,clojure]
----
(def dbsoec {:subprotocol "h2"
             :subname "mem:"
             :isolation-level :serializable})
----

.Or set it when executing a transaction
[source, clojure]
----
(call-in-transaction conn do-something {:isolation-level :serializable})

;; Or...

(with-transaction conn {:isolation-level :serializable}
  (do-something conn))
----

This is a list of supported options:

- `:read-uncommited` - Set read uncommited isolation level
- `:read-commited` - Set read committed isolation level
- `:repeatable-read` - Set repeatable reads isolation level
- `:serializable` - Set serializable isolation level
- `:none` - Use this option to indicate to clojure.jdbc to do nothing and keep default behavior.

You can read more about it on link:http://en.wikipedia.org/wiki/Isolation_(database_systems)[wikipedia].

WARNING: not all JDBC providers support the above isolation levels.


Read-Only Transactions
^^^^^^^^^^^^^^^^^^^^^^

In some circumstances, mainly when you are using the strictest isolation-level, you may want
to indicate to database that a query is actually read-only, allowing the database server to make some
optimizations for this operation.

.You can set transaction read-only using transaction options
[source, clojure]
----
(with-transaction conn {:isolation-level :serializable :read-only true}
  (query-something conn))
----

Advanced usage
--------------

[[cursor-queries]]
Server Side Cursors
~~~~~~~~~~~~~~~~~~~

By default, most JDBC drivers prefetch all results into memory make the use of lazy structures totally useless
for fetching data. Luckily, some databases implement server-side cursors that avoid this behavior.

If you have an extremely large resultset and you want retrieve it and process each item, this is exactly what you need.

For this purpose, _clojure.jdbc_ exposes the `with-query` macro that uses server-side cursors internally
and exposes a lazy seq of records (instead of a fully evaluated vector) in a macro-created context:

[source,clojure]
----
(let [sql ["SELECT id, name FROM people;"]]
  (with-query conn sql results
    (doseq [row results]
      (println row))))
----

[NOTE]
====
The `with-query` macro implicitly ensures that all code inside a created context is executed
in one transaction or subtransaction. This is mandatory because server-side cursors only work
inside one transaction.
====


Low-level Query Interface
~~~~~~~~~~~~~~~~~~~~~~~~~

All functions that execute queries, use the `make-query` function behind the scenes. It is a low-level
interface for access to query functionality.

This function has distinct behavior in comparison with its high-level siblings. It returns a
`jdbc.types.resultset.ResultSet` instance that works as a Clojure persistent map and contains
these keys:

- the `:stmt` key contains a statement instance used to make the query.
- the `:rs` key contains a raw `java.sql.ResultSet` instance.
- the `:data` key contains the results as lazy-seq or vector depending on parameters.


.Example using `make-query` function
[source,clojure]
----
(let [sql    ["SELECT id, name FROM people WHERE age > ?", 2]
      result (make-query conn sql)]
  (doseq [row (:data result)]
    (println row))
  (.close result))
----

[NOTE]
====
You can see the API documentation to find out more about it, but mainly it is
a container that mantains a reference to the original Java JDBC objects
which are used in executing a query.
====

WARNING: `make-query` is a low-level interface and you must be careful when using it.


[[transaction-strategy]]
Transaction Strategy
~~~~~~~~~~~~~~~~~~~~

Transaction strategies in _clojure.jdbc_ are implemented using protocols having default implementations explained
in the previous sections. This approach allows an easy way to extend, customize or completely change a transaction
strategy for your application.

If you want another strategy, you should create a new type and implement the `ITransactionStrategy` protocol.

.Sample dummy transaction strategy.
[source,clojure]
----
(defrecord DummyTransactionStrategy []
  tx/ITransactionStrategy
  (begin! [_ conn opts] conn)
  (rollback! [_ conn opts] conn)
  (commit! [_ conn opts] conn))
----

You can specify the transaction strategy to use in these ways:

.Using the `with-transaction-strategy` macro.
[source,clojure]
----
(with-connection [conn dbspec]
  (with-transaction-strategy conn (DummyTransactionStrategy.)
    (do-some-thing conn)))
----

NOTE: `with-transaction-strategy` does not use dynamic vars, it simple associate the
strategy to connection and exposes it.

.Using the `wrap-transaction-strategy` function:
[source,clojure]
----
(with-open [conn (-> (make-connection dbspec)
                     (wrap-transaction-strategy (DummyTransactionStrategy.)))]
  (do-some-thing conn))
----

.Using dynamic vars
[source, clojure]
----
;; Overwritting the default value
(alter-var-root #'tx/*default-tx-strategy* (fn [_] (DummyTransactionStrategy.)))

;; Or using binding
(binding [tx/*default-tx-strategy* (DummyTransactionStrategy.)]
  (some-func-that-uses-transactions))
----

Extend SQL Types
~~~~~~~~~~~~~~~~

Everything related to type handling/conversion is exposed in the `jdbc.types` namespace.

If you want to extend some type/class to use it as JDBC parameter without explicit conversion
to an SQL-compatible type, you should extend your type with the `jdbc.types/ISQLType` protocol.

Here is an example which extends Java's String[] (string array) in order to pass it as
a query parameter that corresponds to PostgreSQL text array in the database:

[source,clojure]
----
(extend-protocol ISQLType
  ;; Obtain a class for string array
  (class (into-array String []))

  (set-stmt-parameter! [this conn stmt index]
    (let [raw-conn        (:connection conn)
          prepared-value  (as-sql-type this conn)
          array           (.createArrayOf raw-conn "text" prepared-value)]
      (.setArray stmt index array)))

  (as-sql-type [this conn] this))
----

In this way you can pass a string array as a JDBC parameter that is automatically converted
to an SQL array and assigned properly in a prepared statement:

[source,clojure]
----
(with-connection [conn pg-dbspec]
  (execute! conn "CREATE TABLE arrayfoo (id integer, data text[]);")
  (let [mystringarray (into-array String ["foo" "bar"])]
    (execute-prepared! conn "INSERT INTO arrayfoo VALUES (?, ?);"
                       [1, mystringarray])))
----


clojure.jdbc also exposes the `jdbc.types/ISQLResultSetReadColumn` protocol that encapsulates
reverse conversions from SQL types to user-defined types.

[[connection-pool]]
Connection pool
---------------
To make good use of resourses is much recommendable use any style of connection pooling
in your production code. This can avoid continuosly creating and destroying connections,
that in the majority of time is a slow operation.

Java ecosystem comes with various of it, and _clojure.jdbc_ has extensios to few of
them:

- c3p0: http://www.mchange.com/projects/c3p0/
- Apache DBCP2: http://commons.apache.org/proper/commons-dbcp/
- HikariCP: https://github.com/brettwooldridge/HikariCP

Each connection pool / datasource adapter lives in separated package for not installing
unnecesary dependencies if it is not used.


c3p0
~~~~

c3p0, a mature, highly concurrent JDBC connection pooling library for clojure.jdbc.


Install
^^^^^^^

In case of *c3p0*, you should put this on your dependencies vector:

[source, clojure]
----
[clojure.jdbc/clojure.jdbc-c3p0 "0.3.1"]
----


Basic Usage
^^^^^^^^^^^

In order to use a connection pool, you should convert your plain dbspec into a
datasource-dbspec using the helper function provided in each extension.

.Example using c3p0 connection pool
[source,clojure]
----
;; Import the desired implementation
(require '[jdbc.pool.c3p0 :as pool])

;; Convert the standard dbspec to an other dbspec with `:datasource` key
(def dbspec (pool/make-datasource-spec {:subprotocol "postgresql"
                                        :subname "//localhost:5432/dbname"}))
----

Now, dbspec should be used like a plain dbspec for creating connections.


Advanced configuration
++++++++++++++++++++++

c3p0 comes with "good" defaults that should work in majority standard environments,
but obviously, it exposes set of options for customize it:

[options="header"]
|============================================================================
| Option                          | Description
| `:min-pool-size`                | Minimum number of Connections a pool will maintain at any given time.
| `:max-pool-size`                | Maximum number of Connections a pool will maintain at any given time.
| `:initial-pool-size`            | Number of Connections a pool will try to acquire upon startup.
| `:max-wait-millis`              | The number of milliseconds a client calling getConnection() will wait for a Connection to be checked-in or acquired when the pool is exhausted.
| `:max-connection-lifetime`      | The maximum lifetime in seconds of a connection.
| `:test-connection-query`        | The SQL query that will be used to validate connections from this pool before returning them to the caller. If specified, this query MUST be an SQL SELECT statement that returns at least one row. If not specified, connections will be validation by calling the isValid() method.
| `:test-connection-on-borrow`    | The indication of whether objects will be validated before being borrowed from the pool.
| `:test-connection-on-return`    | The indication of whether objects will be validated before being returned to the pool.
| `:test-idle-connections-period` | The number of seconds to sleep between runs of the idle object evictor thread.
| `:max-connection-idle-lifetime` | The minimum amount of time an object may sit idle in the pool before it is eligable for eviction by the idle object evictor (if any).
|============================================================================


dbcp2
~~~~~

Apache commons DBCP (JDBC) connection pool implementation for clojure.jdbc


Install
^^^^^^^

In case of Apache DBCP2, this is a dependency that you should put on your dependencies
vector:

[source, clojure]
----
[clojure.jdbc/clojure.jdbc-dbcp "0.3.1"]
----


Basic Usage
^^^^^^^^^^^

In order to use a connection pool, you should convert your plain dbspec into a
datasource-dbspec using the helper function provided in each extension.

.Example using DBCP connection pool
[source,clojure]
----
;; Import the desired implementation
(require '[jdbc.pool.dbcp :as pool])

;; Convert the standard dbspec to an other dbspec with `:datasource` key
(def dbspec (pool/make-datasource-spec {:subprotocol "postgresql"
                                        :subname "//localhost:5432/dbname"}))
----

Now, dbspec should be used like a plain dbspec for creating connections.


Advanced configuration
^^^^^^^^^^^^^^^^^^^^^^

DBCP comes with "good" defaults that should work in majority standard environments,
but obviously, it exposes set of options for customize it:

[options="header"]
|============================================================================
| Option                          | Description
| `:min-pool-size`                | Minimum number of Connections a pool will maintain at any given time.
| `:max-pool-size`                | Maximum number of Connections a pool will maintain at any given time.
| `:initial-pool-size`            | Number of Connections a pool will try to acquire upon startup.
| `:max-wait-millis`              | The number of milliseconds a client calling getConnection() will wait for a Connection to be checked-in or acquired when the pool is exhausted.
| `:max-connection-lifetime`      | The maximum lifetime in seconds of a connection.
| `:test-connection-query`        | The SQL query that will be used to validate connections from this pool before returning them to the caller. If specified, this query MUST be an SQL SELECT statement that returns at least one row. If not specified, connections will be validation by calling the isValid() method.
| `:test-connection-on-borrow`    | The indication of whether objects will be validated before being borrowed from the pool.
| `:test-connection-on-return`    | The indication of whether objects will be validated before being returned to the pool.
| `:test-idle-connections-period` | The number of seconds to sleep between runs of the idle object evictor thread.
| `:max-connection-idle-lifetime` | The minimum amount of time an object may sit idle in the pool before it is eligable for eviction by the idle object evictor (if any).
|============================================================================


HikariCP
~~~~~~~~
Fast, simple, reliable. HikariCP is a "zero-overhead" production ready JDBC connection pool.


[WARNING]
====
This adapter does not respects the standard dbspec format, it has its own format and
depends completely of used adapter. This is happens because HikariCP  works as some kind of
wrapper and it forward almos all parameters to the wrapped datasource. And each datasource
uses its owns parameter names.

Also, HikariCP adapter targets to only JDK8 version.
====


Install
^^^^^^^

In case of HikariCP, this is a dependency that you should put on your dependencies
vector:

[source, clojure]
----
[clojure.jdbc/clojure.jdbc-hikari "0.3.0"]
----


Basic Usage
^^^^^^^^^^^

In order to use a connection pool, you should convert your plain dbspec into a
datasource-dbspec using the helper function provided in each extension.

.Example using DBCP connection pool
[source,clojure]
----
;; Import the desired implementation
(require '[jdbc.pool.dbcp :as pool])

;; Convert the standard dbspec to an other dbspec with `:datasource` key
(def dbspec (pool/make-datasource-spec {:database-name "test"
                                        :adapter :postgresql}))
----


Advanced configuration
^^^^^^^^^^^^^^^^^^^^^^

HikariCP comes with "good" defaults that should work in majority standard environments,
but obviously, it exposes set of options for customize it:

.Specific generic options of HikariCP
[options="header"]
|============================================================================
| Option                | Description
| `:auto-commit`        | This property controls the default auto-commit behavior of connections returned from the pool. It is a boolean value. (default `true`)
| `:read-only`          | This property controls whether Connections obtained from the pool are in read-only mode by default. (default `false)
| `:connection-timeout` | This property controls the maximum number of milliseconds that a client will wait for a connection from the pool. If this time is exceeded without a connection becoming available, a SQLException will be thrown. 100ms is the minimum value. (default `10000`)
| `:idle-timeout`       | This property controls the maximum amount of time (in milliseconds) that a connection is allowed to sit idle in the pool. (default `60000`)
| `:max-lifetime`       | This property controls the maximum lifetime of a connection in the pool. A value of 0 indicates no maximum lifetime (infinite lifetime). (default `1800000`)
| `:minimum-idle`       | This property controls the minimum number of idle connections that HikariCP tries to maintain in the pool. (default `10`)
| `:maximum-pool-size`  | This property controls the maximum size that the pool is allowed to reach, including both idle and in-use connections. Basically this value will determine the maximum number of actual connections to the database backend. (default `10`)
| `:adapter`            | This property sets the database adapter. Please check <<list-of-hikari-adapters,Adapters and corresponding datasource class names>> for the full list of supported adapters and their datasource class names. (*this parameter is mandatory*)
| `:username`           | This property sets the default authentication username used when obtaining Connections from the underlying driver. (default `nil`)
| `:password`           | This property sets the default authentication password used when obtaining Connections from the underlying driver. (default `nil`)
|============================================================================

HikariCP, unlike other datasource implementations, requires to setup explicitly that adapter should be used. This is a list of
supported adapters:

[[list-of-hikari-adapters]]
.List of adapters supported by HikariCP
[options="header"]
|============================================================================
| Adapter           | Datasource class name
| `:derby`          | `org.apache.derby.jdbc.ClientDataSource`
| `:firebird`       | `org.firebirdsql.pool.FBSimpleDataSource`
| `:db2`            | `com.ibm.db2.jcc.DB2SimpleDataSource`
| `:h2`             | `org.h2.jdbcx.JdbcDataSource`
| `:hsqldb`         | `org.hsqldb.jdbc.JDBCDataSource`
| `:mariadb`        | `org.mariadb.jdbc.MySQLDataSource`
| `:mysql`          | `com.mysql.jdbc.jdbc2.optional.MysqlDataSource`
| `:sqlserver-jtds` | `net.sourceforge.jtds.jdbcx.JtdsDataSource`
| `:sqlserver`      | `com.microsoft.sqlserver.jdbc.SQLServerDataSource`
| `:oracle`         | `oracle.jdbc.pool.OracleDataSource`
| `:pgjdbc-ng`      | `com.impossibl.postgres.jdbc.PGDataSource`
| `:postgresql`     | `org.postgresql.ds.PGSimpleDataSource`
| `:sybase`         | `com.sybase.jdbcx.SybDataSource`
|============================================================================


Examples
^^^^^^^^

.Examples using HikariCP with h2 adapter.
[source, clojure]
----
(def dbspec
  (-> {:adapter :h2
       :url "jdbc:h2:/tmp/test"}
      (pool/make-datasource-spec)))
----

.Example using HikariCP with postgresql adapter.
[source, clojure]
----
;; Convert the standard dbspec to an other dbspec with `:datasource` key
(def dbspec
  (-> {:database-name "test"
       :username "foo"
       :password "secret"
       :server-name "localhost"
       :port-number 5432
       :adapter :postgresql}
      (pool/make-datasource-spec)))
----

How to Contribute?
------------------

**clojure.jdbc** unlike Clojure and other Clojure contrib libs, does not have many
restrictions for contributions. Just follow the following steps depending on the
situation:

**Bugfix**:

- Fork the GitHub repo.
- Fix a bug/typo on a new branch.
- Make a pull-request to master.

**New feature**:

- Open new issue with the new feature proposal.
- If it is accepted, follow the same steps as "bugfix".


FAQ
---

Why another JDBC wrapper?
~~~~~~~~~~~~~~~~~~~~~~~~~

This is an incomplete list of reasons:

- Connection management should be explicit. clojure.jdbc has a clear differentiation
  between connection and dbspec without unnecessary nesting controls and with explicit
  resource management (using `with-open` or other specific macros for it, see the
  examples).
- clojure.jdbc has full support for the whole transactions API, with the ability to set the
  database isolation level and use nested transactions (savepoints).
  It creates a new transaction if no other transaction is active but,
  when invoked within the context of an already-existing transaction, it creates a savepoint.
- clojure.jdbc supports extension or substitution of transaction management if a default
  behavior is not sufficient for you.
- clojure.jdbc has native support for connection pools.
- clojure.jdbc has a simpler implementation than java.jdbc. It has no more
  complexity than necessary for each available function in the public API. +
  As an example:
  * java.jdbc has a lot boilerplate connection management around all functions
    that receive dbspec. It doesn't have well designed connection management. +
    Ex: functions like `create!` can receive plain a dbspec or a connection. If you are
    curious, take a look at the `with-db-connection` implementation of java.jdbc
    and compare it with `with-connection` of clojure.jdbc. You will get a get a good idea of the
    hidden unnecessary complexity found in java.jdbc. +
    java.jdbc has inconsistent connection management. In contrast, with clojure.jdbc,
    a connection should be created explicitly before using any other function that
    requires one connection.

    * java.jdbc has repeated transaction handling on each CRUD method
    (insert!, drop!, etc...). With clojure.jdbc, if you want that some code to run in a
    transaction, you should wrap it in a transaction context explicitly, using the
    `with-transaction` macro (see the transactions section for more information).

- Much more documentation ;) (a project without documentation is a project that doesn't
  really exist).


Does clojure.jdbc have better performance than java.jdbc?
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

Mostly **Yes**, _clojure.jdbc_  by default has better performance than java.jdbc. You can
run the micro benchmark code in your environment with: `lein with-profile bench run`

In my environments, the results are:

[source,text]
----
[3/5.0.5]niwi@niwi:~/clojure.jdbc> lein with-profile bench run
Simple query without connection overhead.
java.jdbc:
"Elapsed time: 673.890131 msecs"
clojure.jdbc:
"Elapsed time: 450.329706 msecs"
Simple query with connection overhead.
java.jdbc:
"Elapsed time: 2490.233925 msecs"
clojure.jdbc:
"Elapsed time: 2239.524395 msecs"
Simple query with transaction.
java.jdbc:
"Elapsed time: 532.151667 msecs"
clojure.jdbc:
"Elapsed time: 475.982354 msecs"
----


Why does clojure.jdbc not include a DSL for working with SQL as java.jdbc 0.3 does?
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

clojure.jdbc is a wrapper for the Java JDBC interface. It doesn't intend to provide helpers
to avoid SQL usage. There are already plenty of DSLs for working with SQL.
clojure.jdbc will not reinvent the wheel.

This is an incomplete list of Clojure DSLs for SQL:

- https://github.com/niwibe/suricatta
- https://github.com/stch-library/sql
- https://github.com/r0man/sqlingvo
- https://github.com/jkk/honeysql


Is this a fork of java.jdbc?
~~~~~~~~~~~~~~~~~~~~~~~~~~~~

No. It is an alternative implementation.


License
-------

clojure.jdbc is writen from scratch and is licensed under Apache 2.0 license:

----
Copyright (c) 2013-2014 Andrey Antukh <niwi@niwi.be>

Licensed under the Apache License, Version 2.0 (the "License")
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
----

You can see the full license in the LICENSE file located in the root of the project
repo.

Additionaly, I want to give thanks to the `java.jdbc` developers for their good
initial work. Some intial ideas for clojure.jdbc are taken from that project.
